decl = mod | use;

mod = keyword_mod _ws ident ';';

ident = ('a'..'z' | 'A'..'Z' | '_')+;

_ws = (' ' | '\t' | '\n')+;

use = keyword_use _ws use_tree ';';

use_tree =
    (simple_path? '::')? '*'
  | (simple_path? '::')? '{' _ws? (use_tree (',' _ws? use_tree)* ','?)? _ws? '}'
  | simple_path (_ws 'as' _ws (ident | '_'))?
  ;

simple_path = '::'? simple_path_segment ('::' simple_path_segment)*;

simple_path_segment = ident | keyword_super | keyword_self | keyword_crate | keyword_current_crate;

keyword_mod = 'mod';
keyword_use = 'use';
keyword_super = 'super';
keyword_self = 'self';
keyword_crate = 'crate';
keyword_current_crate = '$crate';
